# Master Key for Database Encryption
resource "aws_kms_key" "database" {
    description = "KMS Keys for Database Encryption"
    deletion_window_in_days = 7
    enable_key_rotation = true
    policy = data.aws_iam_policy_document.database_key_policy.json

    tags = local.common_tags
}

# Master Key for Application Encryption
resource "aws_kms_key" "application" {
    description = "KMS Keys for Application Encryption"
    deletion_window_in_days = 7
    enable_key_rotation = true
    policy = data.aws_iam_policy_document.application_key_policy.json

    tags = local.common_tags
}

# Aliases
resource "aws_kms_alias" "database" {
    name = "alias/global-banking-database"
    target_key_id = aws_kms_key.database.key_id
}

resource "aws_kms_alias" "application" {
    name = "alias/global-banking-application"
    target_key_id = aws_kms_key.application.key_id
}

# Database Key Policy
data "aws_iam_policy_document" "database_key_policy" {
    statement {
        sid = "Enable IAM User Permissions"
        effect = "Allow"
        principals {
          type = "AWS"
          identifiers = ["arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"]
        }
        actions = ["kms:*"]
        resources = ["*"]
    }

    statement {
        sid = "Allow Aurora Service"
        effect = "Allow"
        principals {
            type = "Service"
            identifiers = ["rds.amazonaws.com"]
        }
        actions = [
            "kms:Encrypt",
            "kms:Decrypt",
            "kms:ReEncrypt*",
            "kms:GenerateDataKey*",
            "kms:DescribeKey"
        ]
        resources = ["*"]
    }

    statement {
        sid = "Allow Backup Service"
        effect = "Allow"
        principals {
            type = "Service"
            identifiers = ["backup.amazonaws.com"]
        }
        actions = [
            "kms:Encrypt",
            "kms:Decrypt",
            "kms:ReEncrypt*",
            "kms:GenerateDataKey*",
            "kms:DescribeKey"
        ]
        resources = ["*"]
    }       

    statement {
        sid = "Enable S3 Service"
        effect = "Allow"
        principals {
          type = "Service"
          identifiers = ["s3.amazonaws.com"]
        }
        actions = [
            "kms:Decrypt",
            "kms:GenerateDataKey*",
            "kms:Encrypt",
            "kms:ReEncrypt*",
            "kms:DescribeKey"
        ]
        resources = ["*"]
    }

    statement {
        sid = "Enable DynamoDB Service"
        effect = "Allow"
        principals {
          type = "Service"
          identifiers = ["dynamodb.amazonaws.com"]
        }
        actions = [
            "kms:Decrypt",
            "kms:GenerateDataKey*",
            "kms:Encrypt",
            "kms:ReEncrypt*",
            "kms:DescribeKey"
        ]
        resources = ["*"]
    }

    statement {
        sid = "Enable CloudTrail Service"
        effect = "Allow"
        principals {
            type = "Service"
            identifiers = ["cloudtrail.amazonaws.com"]
        }
        actions = [
            "kms:Decrypt",
            "kms:GenerateDataKey*",
            "kms:Encrypt",
            "kms:ReEncrypt*",
            "kms:DescribeKey"
        ]
        resources = ["*"]
        condition {
          test = "StringLike"
          variable = "kms:EncryptionContext:aws:cloudtrail:arn"
          values = ["arn:aws:cloudtrail:*:${data.aws_caller_identity.current.account_id}:trail/global-banking-cloudtrail"]
        }
    }

    statement {
        sid = "Enable ELB Service"
        effect = "Allow"
        principals {
            type = "Service"
            identifiers = ["elasticloadbalancing.amazonaws.com"]
        }
        actions = ["kms:*"]
        resources = ["*"]
    }
}

# Application Key Policy
data "aws_iam_policy_document" "application_key_policy" {  
    statement {
        sid = "Enable IAM User Permissions"
        effect = "Allow"
        principals {
          type = "AWS"
          identifiers = ["arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"]
        }
        actions = ["kms:*"]
        resources = ["*"]
    }

    statement {
        sid = "Allow EKS Service"
        effect = "Allow"
        principals {
          type = "Service"
          identifiers = ["eks.amazonaws.com"]
        }
        actions = ["kms:*"]
        resources = ["*"]
    }

    statement {
        sid = "Enable Lambda Service"
        effect = "Allow"
        principals {
          type = "Service"
          identifiers = ["lambda.amazonaws.com"]
        }
        actions = ["kms:*"]
        resources = ["*"]
    }
}   
